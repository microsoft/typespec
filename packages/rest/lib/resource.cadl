import "./http.cadl";
import "../dist/resource.js";

namespace Cadl.Rest.Resource;

@doc("The default error response for resource operations.")
model ResourceError {
  @doc("The error code.")
  code: int32;

  @doc("The error message.")
  message: string;
}

@doc("Dynamically gathers keys of the model type T.")
@copyResourceKeyParameters
model KeysOf<T> {}

@doc("Dynamically gathers parent keys of the model type T.")
@copyResourceKeyParameters("parent")
model ParentKeysOf<T> {}

@doc("Represents operation parameters for resource TResource.")
model ResourceParameters<TResource> {
  ...KeysOf<TResource>;
}

@doc("Represents collection operation parameters for resource TResource.")
model ResourceCollectionParameters<TResource> {
  ...ParentKeysOf<TResource>;
}

interface ResourceRead<TResource, TError> {
  @doc("Gets an instance of the resource.")
  @read(TResource)
  Get(...ResourceParameters<TResource>): TResource | TError;
}

@doc("Resource create operation completed successfully.")
model ResourceCreatedResponse<T> {
  ...Cadl.Http.CreatedResponse;
  @Cadl.Http.body body: T;
}

interface ResourceCreateOrUpdate<TResource, TError> {
  @doc("Creates or update a instance of the resource.")
  @createOrUpdate(TResource)
  Create(
    ...ResourceParameters<TResource>,
    @Cadl.Http.body resource: TResource
  ): TResource | ResourceCreatedResponse<TResource> | TError;
}

interface ResourceCreate<TResource, TError> {
  @doc("Creates a new instance of the resource.")
  @create(TResource)
  Create(
    ...ResourceCollectionParameters<TResource>,
    @Cadl.Http.body resource: TResource
  ): TResource | ResourceCreatedResponse<TResource> | TError;
}

interface ResourceUpdate<TResource, TError> {
  @doc("Updates an existing instance of the resource.")
  @update(TResource)
  Update(
    ...ResourceParameters<TResource>,
    @Cadl.Http.body properties: OptionalProperties<UpdateableProperties<TResource>>
  ): TResource | TError;
}

@doc("Resource deleted successfully.")
model ResourceDeletedResponse {
  @doc("The status code.")
  @Cadl.Http.header
  statusCode: 200;
}

interface ResourceDelete<TResource, TError> {
  @doc("Deletes an existing instance of the resource.")
  @delete(TResource)
  Delete(...ResourceParameters<TResource>): ResourceDeletedResponse | TError;
}

@doc("Paged response")
model Page<T> {
  @doc("The items on this page")
  value: T[];

  @doc("The link to the next page of items")
  nextLink?: string;
}

interface ResourceList<TResource, TError> {
  @doc("Lists all instances of the resource.")
  @list(TResource)
  List(...ResourceCollectionParameters<TResource>): Page<TResource> | TError;
}

interface ResourceInstanceOperations<TResource, TError> mixes ResourceRead<TResource, TError>, ResourceUpdate<TResource, TError>, ResourceDelete<TResource, TError> {}

interface ResourceCollectionOperations<TResource, TError> mixes ResourceCreate<TResource, TError>, ResourceList<TResource, TError> {}

interface ResourceOperations<TResource, TError> mixes ResourceInstanceOperations<TResource, TError>, ResourceCollectionOperations<TResource, TError> {}
