import "./http.cadl";
import "../dist/src/resource.js";
import "../dist/src/internal-decorators.js";

namespace Cadl.Rest.Resource;

using Cadl.Http;

@doc("The default error response for resource operations.")
model ResourceError {
  @doc("The error code.")
  code: int32;

  @doc("The error message.")
  message: string;
}

@doc("Dynamically gathers keys of the model type T.")
@copyResourceKeyParameters
model KeysOf<T> {}

@doc("Dynamically gathers parent keys of the model type T.")
@copyResourceKeyParameters("parent")
model ParentKeysOf<T> {}

@doc("Represents operation parameters for resource TResource.")
model ResourceParameters<TResource> {
  ...KeysOf<TResource>;
}

@doc("Represents collection operation parameters for resource TResource.")
model ResourceCollectionParameters<TResource> {
  ...ParentKeysOf<TResource>;
}

@validateHasKey(TResource)
@validateIsError(TError)
interface ResourceRead<TResource, TError> {
  @autoRoute
  @doc("Gets an instance of the resource.")
  @readsResource(TResource)
  get(...ResourceParameters<TResource>): TResource | TError;
}

@doc("Resource create operation completed successfully.")
model ResourceCreatedResponse<T> {
  ...CreatedResponse;
  @body body: T;
}

interface ResourceCreateOrUpdate<TResource, TError> {
  @autoRoute
  @doc("Creates or update a instance of the resource.")
  @createsOrUpdatesResource(TResource)
  createOrUpdate(
    ...ResourceParameters<TResource>,
    @body resource: TResource
  ): TResource | ResourceCreatedResponse<TResource> | TError;
}

interface ResourceCreate<TResource, TError> {
  @autoRoute
  @doc("Creates a new instance of the resource.")
  @createsResource(TResource)
  create(
    ...ResourceCollectionParameters<TResource>,
    @body resource: TResource
  ): TResource | ResourceCreatedResponse<TResource> | TError;
}

@validateHasKey(TResource)
@validateIsError(TError)
interface ResourceUpdate<TResource, TError> {
  @autoRoute
  @doc("Updates an existing instance of the resource.")
  @updatesResource(TResource)
  update(
    ...ResourceParameters<TResource>,
    @body properties: OptionalProperties<UpdateableProperties<TResource>>
  ): TResource | TError;
}

@doc("Resource deleted successfully.")
model ResourceDeletedResponse {
  @doc("The status code.")
  @statusCode
  _: 200;
}

@validateHasKey(TResource)
@validateIsError(TError)
interface ResourceDelete<TResource, TError> {
  @autoRoute
  @doc("Deletes an existing instance of the resource.")
  @deletesResource(TResource)
  delete(...ResourceParameters<TResource>): ResourceDeletedResponse | TError;
}

@doc("Paged response")
model Page<T> {
  @doc("The items on this page")
  value: T[];

  @doc("The link to the next page of items")
  nextLink?: string;
}

interface ResourceList<TResource, TError> {
  @autoRoute
  @doc("Lists all instances of the resource.")
  @listsResource(TResource)
  list(...ResourceCollectionParameters<TResource>): Page<TResource> | TError;
}

@validateHasKey(TResource)
@validateIsError(TError)
interface ResourceInstanceOperations<TResource, TError>
  mixes ResourceRead<TResource, TError>,
    ResourceUpdate<TResource, TError>,
    ResourceDelete<TResource, TError> {}

@validateHasKey(TResource)
@validateIsError(TError)
interface ResourceCollectionOperations<TResource, TError>
  mixes ResourceCreate<TResource, TError>,
    ResourceList<TResource, TError> {}

@validateHasKey(TResource)
@validateIsError(TError)
interface ResourceOperations<TResource, TError>
  mixes ResourceInstanceOperations<TResource, TError>,
    ResourceCollectionOperations<TResource, TError> {}

@validateHasKey(TResource)
@validateIsError(TError)
interface SingletonResourceRead<TSingleton, TResource, TError> {
  @autoRoute
  @doc("Gets the singleton resource.")
  @segmentOf(TSingleton)
  @readsResource(TSingleton)
  Get(...ResourceParameters<TResource>): TSingleton | TError;
}

@validateHasKey(TResource)
@validateIsError(TError)
interface SingletonResourceUpdate<TSingleton, TResource, TError> {
  @autoRoute
  @doc("Updates the singleton resource.")
  @segmentOf(TSingleton)
  @updatesResource(TSingleton)
  Update(
    ...ResourceParameters<TResource>,
    @body properties: OptionalProperties<UpdateableProperties<TSingleton>>
  ): TSingleton | TError;
}

interface SingletonResourceOperations<TSingleton, TResource, TError>
  mixes SingletonResourceRead<TSingleton, TResource, TError>,
    SingletonResourceUpdate<TSingleton, TResource, TError> {}

@validateHasKey(TResource)
@validateIsError(TError)
interface ExtensionResourceRead<TExtension, TResource, TError> {
  @autoRoute
  @doc("Gets an instance of the extension resource.")
  @readsResource(TExtension)
  Get(...ResourceParameters<TResource>, ...ResourceParameters<TExtension>): TExtension | TError;
}

interface ExtensionResourceCreateOrUpdate<TExtension, TResource, TError> {
  @autoRoute
  @doc("Creates or update a instance of the extension resource.")
  @createsOrUpdatesResource(TExtension)
  CreateOrUpdate(
    ...ResourceParameters<TResource>,
    ...ResourceParameters<TExtension>,
    @body resource: TExtension
  ): TExtension | ResourceCreatedResponse<TExtension> | TError;
}

interface ExtensionResourceCreate<TExtension, TResource, TError> {
  @autoRoute
  @doc("Creates a new instance of the extension resource.")
  @createsResource(TExtension)
  Create(
    ...ResourceParameters<TResource>,
    @body resource: TResource
  ): TExtension | ResourceCreatedResponse<TExtension> | TError;
}

interface ExtensionResourceUpdate<TExtension, TResource, TError> {
  @autoRoute
  @doc("Updates an existing instance of the extension resource.")
  @updatesResource(TExtension)
  Update(
    ...ResourceParameters<TResource>,
    ...ResourceParameters<TExtension>,
    @body properties: OptionalProperties<UpdateableProperties<TExtension>>
  ): TExtension | TError;
}

interface ExtensionResourceDelete<TExtension, TResource, TError> {
  @autoRoute
  @doc("Deletes an existing instance of the extension resource.")
  @deletesResource(TExtension)
  Delete(
    ...ResourceParameters<TResource>,
    ...ResourceParameters<TExtension>
  ): ResourceDeletedResponse | TError;
}

interface ExtensionResourceList<TExtension, TResource, TError> {
  @autoRoute
  @doc("Lists all instances of the extension resource.")
  @listsResource(TExtension)
  List(
    ...ResourceParameters<TResource>,
    ...ResourceCollectionParameters<TExtension>
  ): Page<TExtension> | TError;
}

interface ExtensionResourceInstanceOperations<TExtension, TResource, TError>
  mixes ExtensionResourceRead<TExtension, TResource, TError>,
    ExtensionResourceUpdate<TExtension, TResource, TError>,
    ExtensionResourceDelete<TExtension, TResource, TError> {}

interface ExtensionResourceCollectionOperations<TExtension, TResource, TError>
  mixes ExtensionResourceCreate<TExtension, TResource, TError>,
    ExtensionResourceList<TExtension, TResource, TError> {}

interface ExtensionResourceOperations<TExtension, TResource, TError>
  mixes ExtensionResourceInstanceOperations<TExtension, TResource, TError>,
    ExtensionResourceCollectionOperations<TExtension, TResource, TError> {}
