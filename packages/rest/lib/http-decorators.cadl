namespace Cadl.Http;

using Cadl.Reflection;

/**
 * Specify this property is to be sent or received as an http header.
 *
 * @param headerName Optional name of the header when sent over http.
 *
 * @example
 *
 * ```cadl
 * op read(@header accept: string): {@header("E-Tag") eTag: string};
 * ```
 */
extern dec header(target: ModelProperty, headerName?: string);

/**
 * Specify this property is to be sent as a query parameter.
 *
 * @param queryKey Optional name of the query when included in the url.
 *
 * @example
 *
 * ```cadl
 * op read(@query select: string, @query("order-by") orderBy: string): void;
 * ```
 */
extern dec query(target: ModelProperty, queryKey?: string);

/**
 * Explicitly specify that this property is to be interpolated as a path parameter.
 *
 * @param paramName Optional name of the parmaeter in the url template.
 *
 * @example
 *
 * ```cadl
 * @route("/read/{explicit}/things/{implicit}")
 * op read(@path explicit: string, implicit: string): void;
 * ```
 */
extern dec path(target: ModelProperty, paramName?: string);

/**
 * Explicitly specify that this property is to be set as the body
 *
 * @example
 *
 * ```cadl
 * op upload(@body image: bytes): void;
 * op download(): {@body image: bytes};
 * ```
 */
extern dec body(target: ModelProperty);

/**
 * Specify the status code for this response. Property type must be a status code integer or a union of status code integer.
 *
 * @example
 *
 * ```cadl
 * op read(): {@statusCode: 200, @body pet: Pet}
 * op create(): {@statusCode: 201 | 202}
 * ```
 */
extern dec statusCode(target: ModelProperty);

/**
 * Specify the http verb for the target operation to be `GET`.
 *
 * @example
 *
 * ```cadl
 * @get op read(): string
 * ```
 */
extern dec get(target: Operation);

/**
 * Specify the http verb for the target operation to be `PUT`.
 *
 * @example
 *
 * ```cadl
 * @put op set(pet: Pet): void
 * ```
 */
extern dec put(target: Operation);

/**
 * Specify the http verb for the target operation to be `POST`.
 *
 * @example
 *
 * ```cadl
 * @post op create(pet: Pet): void
 * ```
 */
extern dec post(target: Operation);

/**
 * Specify the http verb for the target operation to be `PATCH`.
 *
 * @example
 *
 * ```cadl
 * @patch op update(pet: Pet): void
 * ```
 */
extern dec patch(target: Operation);

/**
 * Specify the http verb for the target operation to be `DELETE`.
 *
 * @example
 *
 * ```cadl
 * @delete op set(petId: string): void
 * ```
 */
extern dec delete(target: Operation);

/**
 * Specify the http verb for the target operation to be `HEAD`.
 * @example
 *
 * ```cadl
 * @head op ping(petId: string): void
 * ```
 */
extern dec head(target: Operation);

/**
 * Specify the endpoint for this service.
 *  @param url Server endpoint
 *  @param url Description of the endpoint
 *  @param parameters Optional set of parameters used to interpolate the url.
 *
 * @example
 * ```cadl
 * @service
 * @server("https://example.com", "Single server endpoint")
 * namespace PetStore;
 * ```
 *
 * @example parameterized
 *
 * ```cadl
 * @server("https://{region}.foo.com", "Regional endpoint", {
 *   @doc("Region name")
 *   region?: string = "westus",
 * })
 * ```
 *
 */
extern dec server(target: Namespace, url: string, description: string, parameters?: object);

/**
 * Specify this service authentication. See the [documentation in the Http library][https://microsoft.github.io/cadl/standard-library/rest/authentication] for full details.
 *
 * @param auth Authentication configuration. Can be a single security scheme, a union(either option is valid authentication) or a tuple(Must use all authentication together)
 * @example
 *
 * ```cadl
 * @service
 * @useAuth(BasicAuth)
 * namespace PetStore;
 * ```
 */
extern dec useAuth(target: Namespace, auth: object | Union | object[]);

/**
 * Specify if inapplicable metadata should be included in the payload for the given entity.
 */
extern dec includeInapplicableMetadataInPayload(target: unknown, value: boolean);
