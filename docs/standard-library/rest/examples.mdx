---
title: Examples
---

# Examples

Note `Expected client` section shows a general guidance on what the cadl should be represented as a client. It is represented as TypeScript Sync function for simplicity but is in no way tied to it.

## Request parameters

## No parameters

```cadl
op ping(): void;
```

```yaml
url: /
headers:
body:
```

```ts title="Expected client"
function ping(): void;
```

## With query parameters

```cadl
@route("/pets")
op list(@query select: string, @query top: int32): Pet[];
```

```yaml
url: /pets?select=foo,bar&top=100
headers:
body:
```

```ts title="Expected client"
function list(select: string, top: number): Pet[];
```

## With headers parameters

```cadl
op update(@header("If-None-Match") ifNoneMatch: string, @header("Expires") expires: string): void;
```

```yaml
url: /
headers:
  If-None-Match: "abcdef"
  Expires: Wed, 21 Oct 2015 07:28:00 GMT
body:
```

```ts title="Expected client"
function update(ifNoneMatch: string, expires: string): void;
```

## With implicit body parameters

```cadl
op update(name: string, age: int32): void;
```

```yaml
url: /
headers:
body: { "name": "Rex", "age": 3 }
```

```ts title="Expected client"
function update(name: string, age: int32): void;
```

## With explicit body parameters (Using `@body`)

```cadl
model Pet {
  name: string;
  age: int32;
}
op update(@body pet: Pet): void;
```

```yaml
url: /
headers:
body: { "name": "Rex", "age": 3 }
```

```ts title="Expected client"
function update(pet: Pet): void;
```

:::caution
Note: Without `@body` the result would be the same expected client interface but the payload would be nested under a `pet` property.

```yaml
url: /
headers:
body: { pet: { "name": "Rex", "age": 3 } }
```

:::

## With spread model

```cadl
model Pet {
  name: string;
  age: int32;
}
op update(...Pet): void;
```

```yaml
url: /
headers:
body: { "name": "Rex", "age": 3 }
```

```ts title="Expected client"
function update(pet: Pet): void;
```

## With spread model and mixed properties targets

```cadl
model Pet {
  @path id: string;
  name: string;
  age: int32;
  @header("ETag") eTag: string;
}
@route("/pets/{id}") op update(...Pet): void;
```

```yaml
url: /pets/123
headers:
  ETag: abcdef
body: { "name": "Rex", "age": 3 }
```

```ts title="Expected client"
function update(pet: Pet): void;
```

## Many individual parameters

```cadl
@route("/pets/{id}") op update(
  @path id: string,
  name: string,
  age: string,
  @header("ETag") eTag: strin
): void;
```

```yaml
url: /pets/123
headers:
  ETag: abcdef
body: { "name": "Rex", "age": 3 }
```

```ts title="Expected client"
function update(id: string, name: string, age: string, eTag: string): void;
```

## Many individual parameters grouping body

```cadl
model UserContent {
  name: string;
  age: string;
}
@route("/pets/{id}") op update(
  @path id: string,
  @body userContent: UserContent,
  @header("ETag") eTag: string
): void;

// Equivalent to 
alias User = {
  @path id: string;
  @body userContent: UserContent;
  @header("ETag") eTag: string;
};
@route("/pets/{id}") op update(...User): void;
```

```yaml
url: /pets/123
headers:
  ETag: abcdef
body: { "name": "Rex", "age": 3 }
```

```ts title="Expected client"
function update(id: string, userContent: UserContent, eTag: string): void;
```
